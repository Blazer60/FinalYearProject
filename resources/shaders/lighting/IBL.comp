#version 460

#if !defined(WHITE_FURNACE_TEST)
    #define WHITE_FURNACE_TEST 0
#endif

#include "../Colour.glsl"
#if WHITE_FURNACE_TEST > 0
    #define COMPUTE_SHEEN 1
#endif

#if !defined(COMPUTE_SHEEN)
    #define COMPUTE_SHEEN 0
#endif

#if !defined(TILED_RENDERING)
    #define TILED_RENDERING 0
#endif
#if !defined(SHADER_INDEX)
    #define SHADER_INDEX 0
#endif

#if TILED_RENDERING > 0
    #include "../classification/ClassificationBuffer.glsl"
#else
    #define TILE_THREAD_GROUP_SIZE 16
#endif

#include "../geometry/GBuffer.glsl"
#include "../Camera.glsl"
#include "Brdf.glsl"
#include "../Maths.glsl"
#include "Ray.glsl"


layout(binding = 0) uniform sampler2D depthBufferTexture;
layout(binding = 1) uniform sampler2D missingSpecularLutTexture;

#if WHITE_FURNACE_TEST <= 0

layout(binding = 4) uniform samplerCube u_irradiance_texture;
layout(binding = 5) uniform samplerCube u_pre_filter_texture;

uniform float u_luminance_multiplier;

#endif

#if COMPUTE_SHEEN > 0
layout(binding = 6) uniform sampler2D sheenLut;
layout(binding = 7) uniform sampler2D sheenMissing;
#endif

layout(binding = 1, rgba16f) uniform image2D lighting;

// specular missing is split as we don't know f0 until now.
vec3 reconstructSpecularMissing(vec3 f0, float nDotV, float roughness)
{
    const vec3 f0Average = (20.f / 21.f) * f0 + vec3(1.f / 21.f);

    const float avgRspec = directionalAlbedoAverageWhite(vec2(roughness, 0.5f));
    const float oneMinusAvgRspec = 1.f - avgRspec;

    const float thatSummationPartOnTheRight = texture(missingSpecularLutTexture, vec2(nDotV, roughness)).r;

    const vec3 denominator = (vec3(1.f) - f0Average * oneMinusAvgRspec);

    return f0Average / denominator * thatSummationPartOnTheRight;
}

void evaluateIblBaseClosure(in out vec3 colour, Ray viewRay, GBuffer gBuffer)
{
    const vec3 n = gBuffer.normal;
    const float vDotN = saturate(dot(viewRay.direction, n));

    const vec3 fresnel = fresnelSchlick(gBuffer.specular, vDotN);
    const vec2 lut = texture(directionalAlbedoLut, vec2(vDotN, gBuffer.roughness)).rg;
    const vec3 specular = (fresnel * lut.x + lut.y);
    const vec3 specMissing = reconstructSpecularMissing(fresnel, vDotN, gBuffer.roughness);
    const vec3 fullSpecular = specular + specMissing;

    // todo: remove dependancy to Brdf.glsl => uniforms are getting too complicated to track.
    const vec3 diffuse = evaluateDiffuseBrdf(gBuffer, fullSpecular);

#if WHITE_FURNACE_TEST > 0
    const vec3 preFilterColour = vec3(1.f);

    const vec3 irradiance = vec3(1.f);
#else
    const vec3 r = reflect(-viewRay.direction, n);
    const float maxReflectionLod = textureQueryLevels(u_pre_filter_texture);
    const vec3 preFilterColour = textureLod(u_pre_filter_texture, r, gBuffer.roughness * maxReflectionLod).rgb * u_luminance_multiplier;

    const vec3 irradiance = texture(u_irradiance_texture, n).rgb * u_luminance_multiplier;
#endif

    const vec3 backColour = fullSpecular * preFilterColour + diffuse * irradiance;

    colour = viewRay.coverage * (colour + viewRay.transmittance * backColour) + (1.f - viewRay.coverage) * backColour;
}

vec3 evaluateIblTopSpecularClosure(Ray viewRay, GBuffer gBuffer)
{
    const vec3 n = gBuffer.topNormal;
    const float vDotN = saturate(dot(viewRay.direction, n));

    const vec3 fresnel = fresnelSchlick(gBuffer.topSpecular, vDotN);
    const vec2 lut = texture(directionalAlbedoLut, vec2(vDotN, gBuffer.topRoughness)).rg;
    const vec3 specular = (fresnel * lut.x + lut.y);
    const vec3 specMissing = reconstructSpecularMissing(fresnel, vDotN, gBuffer.topRoughness);
    const vec3 fullSpecular = specular + specMissing;

#if WHITE_FURNACE_TEST > 0
    const vec3 preFilterColour = vec3(1.f);
#else
    const vec3 r = reflect(-viewRay.direction, n);
    const float maxReflectionLod = textureQueryLevels(u_pre_filter_texture);
    const vec3 preFilterColour = textureLod(u_pre_filter_texture, r, gBuffer.topRoughness * maxReflectionLod).rgb * u_luminance_multiplier;
#endif

    return fullSpecular * preFilterColour;
}

void evaluateIblTopClosure(in out vec3 colour, in out Ray viewRay, GBuffer gBuffer)
{
    if (gBufferHasFlag(gBuffer, GBUFFER_FLAG_TRANSMITTANCE_BIT) == 1)
    {
        colour = evaluateIblTopSpecularClosure(viewRay, gBuffer);

        const vec3 extinctionCoefficient = -log(gBuffer.transmittance + vec3(0.0001f)) / max(gBuffer.topThickness, MIN_THRESHOLD);
        const float distance = 0.01f / max(dot(gBuffer.topNormal, viewRay.direction), 0.0001f);
        // The actual thickness is a constant of 1cm.

        viewRay.transmittance = exp(-extinctionCoefficient * distance);
        viewRay.coverage = gBuffer.topCoverage;

        // Refract the view ray, change position and direction.
        const float glassRefractiveIndex = mix(1.f, 1.5f, viewRay.coverage);// Todo: Make this a gBuffer parameter.
        viewRay.direction = -refract(-viewRay.direction, gBuffer.topNormal, 1.f / glassRefractiveIndex);
        viewRay.position = viewRay.position - viewRay.direction * distance;
        viewRay.refractiveIndex = glassRefractiveIndex;
    }
}

void evaluateIblSheenCoating(in out vec3 colour, GBuffer gBuffer, vec3 v)
{
#if COMPUTE_SHEEN > 0
    vec3 sheen = vec3(0.f);
    float sheenScalar = 1.f;

    const vec3 n = gBuffer.normal;
    const float vDotN = saturate(dot(v, n));

    if (gBufferHasFlag(gBuffer, GBUFFER_FLAG_FUZZ_BIT) == 1)
    {
    #if WHITE_FURNACE_TEST > 0
        sheen = texture(sheenLut, vec2(vDotN, gBuffer.fuzzRoughness)).rrr;
        sheenScalar = texture(sheenMissing, vec2(vDotN, gBuffer.fuzzRoughness)).r;
    #else
        sheen = gBuffer.fuzzColour * texture(sheenLut, vec2(vDotN, gBuffer.fuzzRoughness)).r;
        sheenScalar = texture(sheenMissing, vec2(vDotN, gBuffer.fuzzRoughness)).r;
    #endif
    }

    #if WHITE_FURNACE_TEST > 0
    const vec3 irradiance = vec3(1.f);
    #else
    const vec3 irradiance = texture(u_irradiance_texture, n).rgb * u_luminance_multiplier;
    #endif
    colour = sheen * irradiance + sheenScalar * colour;
#endif
}

vec3 evaluateBxDF(GBuffer gBuffer, vec3 position)
{
    const vec3 viewDirection = normalize(camera.position - position);

    vec3 colour = vec3(0.f);
    Ray viewRay;
    viewRay.direction = viewDirection;
    viewRay.position = position;
    viewRay.refractiveIndex = 1.f;  // Air.
    viewRay.transmittance = vec3(1.f);
    viewRay.coverage = 0.f;

    evaluateIblTopClosure(colour, viewRay, gBuffer);
    evaluateIblBaseClosure(colour, viewRay, gBuffer);
    evaluateIblSheenCoating(colour, gBuffer, viewDirection);

    return colour;
}

layout(local_size_x = TILE_THREAD_GROUP_SIZE, local_size_y = TILE_THREAD_GROUP_SIZE, local_size_z = 1) in;
void main()
{
#if TILED_RENDERING > 0
    const uint tileIndex = gl_WorkGroupID.x;
    const uint tileBufferResult = tileBuffers[tileIndex][SHADER_INDEX];
    const uvec2 workGroupId = uvec2(tileBufferResult & 0x0000FFFFu, (tileBufferResult & 0xFFFF0000u) >> 16);
    const uvec2 pixelCoord = workGroupId * gl_WorkGroupSize.xy + gl_LocalInvocationID.xy;
    const vec2 id = vec2(pixelCoord);
#else
    const vec2 id = gl_GlobalInvocationID.xy;
#endif

    const vec2 uv = (id - vec2(0.5f)) / textureSize(depthBufferTexture, 0);
    const float depth = texture(depthBufferTexture, uv).r;
    const vec3 position = positionFromDepth(uv, depth);

    GBuffer gBuffer = pullFromStorageGBuffer(ivec2(id));

    if (gBufferIsValid(gBuffer) == 0)
        return;

    const vec3 colour = imageLoad(lighting, ivec2(id)).rgb;
    const vec3 outColour = colour + camera.exposure * evaluateBxDF(gBuffer, position);
    imageStore(lighting, ivec2(id), vec4(outColour, 1.f));

#if WHITE_FURNACE_TEST > 0
    imageStore(lighting, ivec2(id), vec4(1.f, 0.f, 1.f, 1.f));
    return;
#endif
}

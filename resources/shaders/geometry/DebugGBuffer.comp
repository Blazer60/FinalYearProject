#version 460 core

#include "../interfaces/DebugGBufferBlock.h"
#include "GBuffer.glsl"
#include "../Colour.glsl"

layout(binding = 1, rgba16f) uniform image2D debug;

vec3 byteCountToColour(uint byteCount)
{
    if (byteCount <= 4 * BYTES)
        return vec3(0.f, 0.62f, 0.76f);
    if (byteCount <= 8 * BYTES)
        return vec3(0.f, 0.76f, 0.28f);
    if (byteCount <= 12 * BYTES)
        return vec3(0.76f, 0.73f, 0.f);
    if (byteCount <= 16 * BYTES)
        return vec3(0.76f, 0.4f, 0.f);

    return vec3(0.76f, 0.f, 0.f);
}

layout(local_size_x = DEBUG_GBUFFER_THREAD_SIZE, local_size_y = DEBUG_GBUFFER_THREAD_SIZE, local_size_z = 1) in;
void main()
{
    const ivec2 coord = ivec2(gl_GlobalInvocationID.xy);
    GBuffer gBuffer = pullFromStorageGBuffer(coord);
    vec4 result = cDebug.defaultValue;

    if (cDebug.id == DEBUG_GBUFFER_NORMAL)
        result.xyz = gBuffer.normal;
    if (cDebug.id == DEBUG_GBUFFER_ROUGHNESS)
        result.xyz = gBuffer.roughness.xxx;
    if (cDebug.id == DEBUG_GBUFFER_DIFFUSE)
        result.xyz = gBuffer.diffuse;
    if (cDebug.id == DEBUG_GBUFFER_SPECULAR)
        result.xyz = gBuffer.specular;
    if (cDebug.id == DEBUG_GBUFFER_EMISSIVE)
        result.xyz = gBuffer.emissive;
    if (cDebug.id == DEBUG_GBUFFER_BYTE_COUNT)
        result.xyz = byteCountToColour(gBuffer.byteCount);

    if (cDebug.gammaCorrect != 0)
        result.xyz = linearToSRgb(result.xyz);

    imageStore(debug, coord, result);
}

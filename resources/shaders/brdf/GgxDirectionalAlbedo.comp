#version 460 core

#include "Hammersley.glsl"

layout(binding = 0) writeonly uniform image2D lut;

const float PI = 3.14159265359;

vec3 importanceSampleGgx(vec2 xI, vec3 N, float roughness)
{
    const float a = roughness * roughness;

    const float phi = 2.f * PI * xI.x;
    const float cosTheta = sqrt((1.f - xI.y) / (1.f + (a * a - 1.f) * xI.y));
    const float sinTheta = sqrt(1.f - cosTheta * cosTheta);

    const vec3 H = vec3(cos(phi) * sinTheta, sin(phi) * sinTheta, cosTheta);

    const vec3 up = abs(N.z) < 0.999f ? vec3(0.f, 0.f, 1.f) : vec3(1.f, 0.f, 0.f);
    const vec3 tangent = normalize(cross(up, N));
    const vec3 biTangent = cross(N, tangent);

    const vec3 sampleVec = tangent * H.x + biTangent * H.y + N * H.z;
    return normalize(sampleVec);
}

float geometrySchlickGgx(float nDotV, float roughness)
{
    const float r = roughness;
    const float k = r * r / 2.f;

    return nDotV / (nDotV * (1.f - k) + k);
}

float geometrySmith(vec3 N, vec3 V, vec3 L, float roughness)
{
    const float nDotV = max(dot(N, V), 0.f);
    const float nDotL = max(dot(N, L), 0.f);
    const float ggx2 = geometrySchlickGgx(nDotV, roughness);
    const float ggx1 = geometrySchlickGgx(nDotL, roughness);

    return ggx1 * ggx2;
}

vec2 integrateGgxDirectionalAlbedo(float nDotV, float roughness)
{
    const vec3 V = vec3(sqrt(1.f - nDotV * nDotV), 0.f, nDotV);

    float A = 0.f;
    float B = 0.f;

    vec3 N = vec3(0.f, 0.f, 1.f);

    const uint sampleCount = 1024u;
    for (uint i = 0u; i < sampleCount; ++i)
    {
        const vec2 xI = hammersley(i, sampleCount);
        const vec3 H = importanceSampleGgx(xI, N, roughness);
        const vec3 L = normalize(2.f * dot(V, H) * H - V);

        const float nDotL = max(L.z, 0.f);
        const float nDotH = max(H.z, 0.f);
        const float vDotH = max(dot(V, H), 0.f);

        if (nDotL > 0.f)
        {
            const float G = geometrySmith(N, V, L, roughness);
            const float gVis = (G * vDotH) / (nDotH * nDotV);
            const float fC = pow(1.f - vDotH, 5.f);

            A += (1.f - fC) * gVis;
            B += fC * gVis;
        }
    }

    A /= float(sampleCount);
    B /= float(sampleCount);

    return vec2(A, B);
}

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;
void main()
{
    const vec3 id = gl_GlobalInvocationID;
    const vec3 dispatchSize = gl_NumWorkGroups * gl_WorkGroupSize;
    const vec2 uv = (id.xy + vec2(0.5f)) / dispatchSize.xy;
    const vec2 result = integrateGgxDirectionalAlbedo(uv.x, uv.y);
    imageStore(lut, ivec2(id.xy), vec4(result.xy, 0.f, 0.f));
}
